/*
 * Copyright (C) 2013 JFrog Ltd.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  repositories {
    jcenter()
    dependencies {
      classpath(group: 'org.jfrog.buildinfo', name: 'build-info-extractor-gradle', version: '3.1.1')
    }
  }
}

import java.text.SimpleDateFormat

def globalVersion = new Version(currentVersion)

allprojects {
  apply plugin: 'com.jfrog.artifactory-upload'
  apply plugin: 'idea'
  configure(subprojects.findAll { new File(it.projectDir, 'src').directory }) {
    apply plugin: 'java'
  }
  group = 'org.jfrog.example.gradle'
  version = globalVersion
  status = version.status
}

artifactoryPublish.skip = true

subprojects {
  apply plugin: 'maven'

  if (project.plugins.hasPlugin('java')) {
    //manifest.mainAttributes(provider: 'gradle')
	manifest {
		attributes 'provider': 'gradle'
	}
    configurations {
      published
    }
    dependencies {
      testCompile 'junit:junit:4.7'
    }
    task sourceJar(type: Jar) {
      from sourceSets.main.allSource
      classifier = 'sources'
    }
    task javadocJar(type: Jar, dependsOn: javadoc) {
      classifier = 'javadoc'
      from javadoc.destinationDir
    }
    artifactoryPublish {
      dependsOn sourceJar, javadocJar
    }
    artifacts {
      published sourceJar
      published javadocJar
    }
  }
}

configurations {
  published
}

artifactory {
  contextUrl = 'http://localhost:8081/artifactory'
  publish {
    repository {
      repoKey = 'gradle-snapshot-local' // The Artifactory repository key to publish to
      username = "${artifactory_user}" // The publisher user name
      password = "${artifactory_password}" // The publisher password
      ivy {
        // Optional section for configuring Ivy publication (when publishIvy = true).
        // Assumes Maven repo layout if If not specified.
        ivyLayout = '[organization]/[module]/ivy-[revision].xml'
        artifactLayout = '[organization]/[module]/[revision]/[module]-[revision](-[classifier]).[ext]'
        mavenCompatible = false //Convert any dots in an [organization] layout value to path separators, similar to Maven's groupId-to-path conversion. True if not specified
      }
    }
    defaults {
      // Reference to Gradle configurations defined in the build script.
      // This is how we tell the Artifactory Plugin which artifacts should be
      // published to Artifactory.
      publishConfigs('archives', 'published')
      properties = ['build.status': "$it.project.status".toString()]
      publishPom = false //Publish generated POM files to Artifactory (true by default)
      publishIvy = true //Publish generated Ivy descriptor files to Artifactory (true by default)
    }
  }
  resolve {
    repository {
      repoKey = 'libs-release' //The Artifactory (preferably virtual) repository key to resolve from
      username = "${artifactory_user}" //Optional resolver user name (leave out to use anonymous resolution)
      password = "${artifactory_password}" //The resolver password
      maven = true     
    }
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '2.0'
}

class Version {
  String originalVersion
  String thisVersion
  String status
  Date buildTime

  Version(String versionValue) {
    buildTime = new Date()
    originalVersion = versionValue
    if (originalVersion.endsWith('-SNAPSHOT')) {
      status = 'integration'
      thisVersion = originalVersion.substring(0, originalVersion.length() - 'SNAPSHOT'.length()) + getTimestamp()
    } else {
      status = 'release'
      thisVersion = versionValue
    }
  }

  String getTimestamp() {
    // Convert local file timestamp to UTC
    def format = new SimpleDateFormat('yyyyMMddHHmmss')
    format.setCalendar(Calendar.getInstance(TimeZone.getTimeZone('UTC')));
    return format.format(buildTime)
  }

  String toString() {
    thisVersion
  }
}
